import processing.serial.*;

int bgcolor;
int fgcolor;
Serial myPort;
int[] serialInArray = new int[3];
int serialCount = 0;
int xpos, ypos;
boolean firstContact = false;

void setup() {
  size(256, 256);
  noStroke();
  xpos = width/2;
  ypos = height/2;

  // Processing 2.1 or later, use Serial.printArray()
  println(Serial.list());
  String portName = Serial.list()[0];
  myPort = new Serial(this, portName, 9600);
}
void draw() {
  background(bgcolor);
  fill(fgcolor);
  ellipse(xpos, ypos, 20, 20);
}
void serialEvent(Serial myPort) {
  int inByte = myPort.read();
  if (firstContact == false) {
    if (inByte == 'A') {
      myPort.clear();
      firstContact = true;
      myPort.write('A');       // ask for more
    }
  }
  else {
    serialInArray[serialCount] = inByte;
    serialCount++;

    if (serialCount > 2 ) {
      xpos = serialInArray[0];
      ypos = serialInArray[1];
      fgcolor = serialInArray[2];
      println(xpos + "\t" + ypos + "\t" + fgcolor);

      myPort.write('A');
      serialCount = 0;
    }
  }
}

